plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

def user = findProperty("repo.skykingdoms.user")
def pass = findProperty("repo.skykingdoms.pass")

allprojects {
    group 'me.vihara.core'
    version '1.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        mavenCentral()
        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            url = "https://repo.skykingdoms.net/repository/maven-releases/"

            credentials {
                username = user
                password = pass
            }
        }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:20.1.0'
        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    }

    tasks {
        shadowJar {
            mergeServiceFiles()

            finalizedBy(publishToMavenLocal)
        }

        build {
            dependsOn(shadowJar)
        }

        compileJava {
            targetCompatibility = JavaVersion.VERSION_11
            sourceCompatibility = JavaVersion.VERSION_11
            options.encoding = "UTF-8"
        }
    }

    publishing {
        repositories {
            maven {
                credentials {
                    username = user
                    password = pass
                }

                url = "https://repo.skykingdoms.net/repository/maven-snapshots/"
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

subprojects {
    dependencies {
        compileOnly rootProject
    }
}

dependencies {
    compileOnly group: 'org.spigotmc', name: 'spigot-api', version: '1.8.8-R0.1-SNAPSHOT'
}